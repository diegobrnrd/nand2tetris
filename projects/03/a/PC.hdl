// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    Inc16 (in=feedback, out=f1);
    // else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
    Mux16 (a=feedback, b=f1, sel=inc, out=f2);
    // else if (load[t] == 1)  out[t+1] = in[t]
    Mux16 (a=f2, b=in, sel=load, out=f3);
    // if      (reset[t] == 1) out[t+1] = 0
    Mux16 (a=f3, b=false, sel=reset, out=f4);
    // else                    out[t+1] = out[t]
    Register (in=f4, load=true, out=out, out=feedback);
}
